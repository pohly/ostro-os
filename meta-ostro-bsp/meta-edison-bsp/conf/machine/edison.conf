#@TYPE: Machine
#@NAME: edison
#@DESCRIPTION: Machine configuration for edison systems

DEFAULTTUNE ?= "edison"
X86ARCH32 ?= "i686"

require conf/machine/include/x86/arch-x86.inc

TUNEVALID[edison] = "Enable Edison specific processor optimizations"
TUNE_CCARGS .= "${@bb.utils.contains('TUNE_FEATURES', 'edison', ' -march=atom -msse3 -mfpmath=sse', '', d)}"

AVAILTUNES += "edison"
TUNE_FEATURES_tune-edison = "${TUNE_FEATURES_tune-x86} edison"
BASE_LIB_tune-edison = "lib"
TUNE_PKGARCH_tune-edison = "edison"
# Edison is already in package archs
#PACKAGE_EXTRA_ARCHS_tune-edison = "${PACKAGE_EXTRA_ARCHS_tune-x86} edison"


MACHINE_FEATURES = "bluetooth alsa pci serial usbgadget usbhost wifi x86 ext3"

IMAGE_CLASSES += "image_types_edison"
IMAGE_FSTYPES = "tar.bz2 ext4 toflash"

# we want rootfs size of 1572864
# rootfs_rpm.bbclass adds 51200
# so adjust to 1572864 - 51200
IMAGE_ROOTFS_SIZE = "1521664"
# Also, Ostro OS is not allowed to ask for additional space either.
# TODO: this should be a variable from OE-core which gets
# added by rootfs_rpm.bbclass, instead of doing the increase twice
# and having to substract the 51200 value above.
OSTRO_IMAGE_ROOTFS_EXTRA_SPACE = ""

# Bootloader config

PREFERRED_PROVIDER_virtual/bootloader ?=  "u-boot-edison"
PREFERRED_VERSION_u-boot-edison ?= "2014.04-1"
PREFERRED_VERSION_u-boot-fw-utils-edison ?= "2014.04-1"
UBOOT_MACHINE = "edison_config"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "u-boot-edison u-boot-fw-utils-edison kernel-modules sst-fw mcu-fw-bin mcu-fw-load"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "bluetooth-rfkill-event"

# Kernel config

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto-edison"
PREFERRED_VERSION_linux-yocto-edison = "3.10%"
KERNEL_IMAGETYPE = "bzImage"
SERIAL_CONSOLES ?= "115200;ttyMFD2 115200;ttyGS0"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "kernel-module-bcm-bt-lpm bcm43340-bt"
KERNEL_MODULE_AUTOLOAD += "bcm_bt_lpm"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "bcm43340-mod bcm43340-fw"
KERNEL_MODULE_AUTOLOAD  += "bcm4334x"
KERNEL_MODULE_PROBECONF += "bcm4334x"
module_conf_bcm4334x = "options bcm4334x firmware_path=/etc/firmware/fw_bcmdhd.bin nvram_path=/etc/firmware/bcmdhd.cal"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "kernel-module-g-multi"
KERNEL_MODULE_AUTOLOAD  += "g_multi"
KERNEL_MODULE_PROBECONF += "g_multi"
# FIXME: file parameter should be based on partition UUID (from U-Boot) or fixed
# with label (label seems to work, but driver is probed too early)
module_conf_g_multi = "options g_multi file=/dev/mmcblk0p9 stall=0 idVendor=0x8087 idProduct=0x0A9E iProduct=Edison iManufacturer=Intel"

XSERVER = "xserver-xorg \
           xf86-input-keyboard \
           xf86-input-evdev \
           xf86-video-dummy"

#INITRAMFS_IMAGE ?= "core-image-initramfs-edison"
#INITRAMFS_IMAGE_BUNDLE ?= "1"
